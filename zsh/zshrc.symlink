# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/src/dotfiles

eval "$(docker-machine env default)"

# Make sure we use vim keybindings
bindkey -v
bindkey '\e[3~' delete-char
bindkey '^R' history-incremental-search-backward

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# added by travis gem
[ -f /Users/vincent/.travis/travis.sh ] && source /Users/vincent/.travis/travis.sh

### Added by the Heroku Toolbelt
export PATH="/usr/local/heroku/bin:$PATH"

# vagrant fleetctl hack
export FLEETCTL_TUNNEL=127.0.0.1:2222

# Setting SSL cert file path
SSL_CERT_FILE=/opt/boxen/homebrew/etc/openssl/cacert.pem
